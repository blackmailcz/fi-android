def VERSION_MAJOR = 0
def VERSION_MINOR = 1
def VERSION_PATCH = 0
def VERSION_BUILD = 0

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "$project.name-$variant.name-$variant.versionName-${variant.versionCode}.apk")
        }
    }

    defaultConfig {
        applicationId "uco_448237.movio.pv526.fi.muni.cz.ukol3"
        minSdkVersion 15
        targetSdkVersion 23

        resConfigs "cs", "en"
        resConfigs "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"

        versionCode VERSION_MAJOR * 10000 + VERSION_MINOR * 1000 + VERSION_PATCH * 100 + VERSION_BUILD
        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            //versionCode "${VERSION_MAJOR}:${VERSION_MINOR}:${VERSION_PATCH}:${VERSION_BUILD}"
            // Sorry ale proste to prej nejde takhle staticky menit ten kod verze ...
        }
    }

    productFlavors {
        primary {
            applicationId "uco_448237.movio.pv256.fi.muni.cz.ukol3"
            buildConfigField "boolean", "logging", "true"
        }

        secondary {
            applicationId "uco_448237.movio.pv256.fi.muni.cz.ukol3.secondary"
            buildConfigField "boolean", "logging", "false"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}




dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'

}